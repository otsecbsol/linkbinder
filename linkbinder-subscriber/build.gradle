/*
 * Copyright 2016 OPEN TONE Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'idea'
description = 'linkbinder-subscriber'

configurations {
    subscriber
}

dependencies {
    compile project(':linkbinder-core')
    testCompile project(':linkbinder-framework-test')

    // executable jar起動時に必要なライブラリ
    compile 'args4j:args4j:2.33'
}

// classpathに追加する
tasks.withType(JavaCompile) {
    classpath += configurations.subscriber
}

javadoc {
    classpath += configurations.subscriber
}

idea {
    module {
        scopes.COMPILE.plus += [configurations.subscriber]
    }
}
eclipse {
    classpath {
        plusConfigurations += [configurations.subscriber]
    }
}
jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it).matching { exclude 'META-INF/spring.handlers' } }
    }
    from(file("src/main/resources/spring.handlers")) {
        into "META-INF"
    }
    manifest {
        attributes 'Main-Class' : 'jp.co.opentone.bsol.linkbinder.Application'
        // 依存ライブラリの署名ファイルを取り除く設定
        exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    }
}
