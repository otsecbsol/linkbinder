
/*
 * Copyright 2016 OPEN TONE Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'war'
apply plugin: 'idea'
description = 'linkbinder-web'

configurations {
    jetty
}

dependencies {
    compile project(':linkbinder-framework-web')
    compile project(':linkbinder-core')

    testCompile project(':linkbinder-framework-test')

    // executable war起動時に必要なライブラリ
    jetty 'args4j:args4j:2.33'
    jetty 'org.eclipse.jetty:jetty-runner:9.2.0.v20140526'
}

// classpathにjettyを追加する
tasks.withType(JavaCompile) {
    classpath += configurations.jetty
}

javadoc {
    classpath += configurations.jetty
}

idea {
    module {
        scopes.COMPILE.plus += [configurations.jetty]
    }
}
eclipse {
    classpath {
        plusConfigurations += [configurations.jetty]
    }
}

war {
    manifest {
        attributes 'Main-Class' : 'jp.co.opentone.bsol.linkbinder.Application'

        // 依存ライブラリの署名ファイルを取り除く設定
        exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    }
    from {
        configurations.jetty.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    from fileTree(dir: 'build/classes/main', include: '**/Application.class')
}
